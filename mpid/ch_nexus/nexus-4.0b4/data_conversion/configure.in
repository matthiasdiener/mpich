dnl
dnl rcsid = $Header: /nfs/globus1/src/master/nexus_dc/configure.in,v 1.9 1996/12/12 12:04:38 tuecke Exp $
dnl
AC_INIT(nexus_dc.h.in)
AC_PREREQ(2.10)

LAC_INIT
LAC_NEXUS_INIT
LAC_BUILD_LIBDIR

LAC_PATH_THREADS
LAC_PROG_CC
AC_C_CROSS
AC_PROG_CPP

AC_C_BIGENDIAN
AC_CHECK_SIZEOF(short)
AC_CHECK_SIZEOF(long)

case "$ac_cv_sizeof_long-$ac_cv_c_bigendian" in
    "4-no" )
	MY_FORMAT=32BIT_LE
	MY_FILE=32le
	;;
    "4-yes" )
	MY_FORMAT=32BIT_BE
	MY_FILE=32be
	;;
    "8-no" )
	MY_FORMAT=64BIT_LE
	MY_FILE=64le
	;;
    "8-yes" )
	if test "$ac_cv_sizeof_short" = "2" ; then
	    MY_FORMAT=64BIT_BE
	    MY_FILE=64be
	else
	    MY_FORMAT=CRAYC90
	    MY_FILE=cray
	fi
	;;
esac

AC_SUBST(MY_FORMAT)
AC_SUBST(MY_FILE)

if test "$GCC" = yes -a "$ac_cv_prog_gcc_shared" = no; then
  lac_build_shared="no"
fi

if test "$lac_build_shared" = yes; then
  LAC_SHARED_LIB
  lib_target=shared_lib
  LIBEXT=$SOEXT
else
  lib_target=static_lib
  LIBEXT=a
fi
AC_SUBST(lib_target)
AC_SUBST(LIBEXT)

SUBDIRS="test"
AC_SUBST(SUBDIRS)
AC_CONFIG_SUBDIRS($SUBDIRS)

LAC_MAKEFILES(makefile_header, Makefile)

AC_OUTPUT($lac_makefiles $lac_makefile_headers nexus_dc.h, 
  LAC_INSERT_MAKEFILE_HEADERS echo timestamp > stamp-h,
  [lac_makefiles="$lac_makefiles"]
)
