#! /bin/csh
#
# Run goptest to get communication performance and scalability
#
# Set defaults
set MAXNP   = 8
set SHORT   = 0
set fname   = "-fname gop.out"
set system  = "MPI"
set NATIVE  = ""
set BCAST   = 1
set DSUM    = 1
set SYNC    = 1
set MULT    = 2
set ADD     = 0
set PLOT    = "Cit"
if ($#argv > 1) then
    if ("$1" == "-help") then 
        echo "Run goptest for collective operations"
	echo "Output goes to stdout"
	echo " "
	echo "goptest [ -help ] [ -short ] [ -maxnp n ] [ -fname file ]"
	echo " [ -gnuplot ] [ -sync ] [ -dsum ] [ -bcast ]"
        echo " [ -add ]"
        echo " An fname of -stdout will write all output to stdout"
	echo " -add choses nodes of the form 2*k instead of 2**k"
        exit
    endif
    while ( $#argv > 0 ) 
        switch ($1)

	case -short:
	shift
	set SHORT = 1
	breaksw

	case -maxnp:
	shift
	set MAXNP = $1
	shift
	breaksw

	case -fname:
	shift
        if ( "$1" == "-stdout" ) then
	    set fname = ""
	else 
	    set fname = "-fname $1"
	endif
	shift
	breaksw

	case -gnuplot:
	shift
	set fname = "-gnuplot $fname"
	set PLOT  = "gnuplot"
	breaksw

	case -sync:
	shift
	set DSUM    = 0
	set BCAST   = 0
	breaksw

	case -bcast:
	shift
	set SYNC = 0
	set DSUM = 0
	breaksw
	
	case -dsum:
	shift
	set SYNC = 0
	set BCAST = 0
	breaksw

	case -add:
	shift
	set ADD = 2
	set MULT = 1
	breaksw 
	default:
	echo "Unknown option $1"
	shift 

	endsw
    end
endif
#
# SP1 systems require this and it does not hurt other systems
setenv PWD `pwd | sed 's%/tmp_mnt/%/%g'`
set GRAPH   = "$fname"
set GRAPHHEAD = ""
set GRAPHTAIL = "-notail"
if ( $SHORT == 0 && $PLOT == "Cit") then
    set GRAPH11 = "-wx 1 2 -wy 2 2 $fname"
    set GRAPH12 = "-wx 2 2 -wy 2 2 $fname"
    set GRAPH21 = "-wx 1 2 -wy 1 2 $fname"
    set GRAPH22 = "-wx 2 2 -wy 1 2 $fname -lastwindow"
else
    set GRAPH11 = "$fname"
    set GRAPH12 = "$fname"
    set GRAPH21 = "$fname"
    set GRAPH22 = "$fname"
endif
set LIMITS  = "-dtime 0:15 -cpu 15 -mem 4"
#
# Choose the system to run with ...
set NP = 2
while ($NP <= $MAXNP) 
    setenv MP_PROCS $NP
    set GOPTEST = "mpirun -np $NP goptest"
    set MPP = ""
    #
    # Begin the tests
    #
    # Round-trip times, blocking (from 0 to 1 MBytes)
    if ( $SYNC == 1 ) then
        if ($NP == 2) echo "# Synchronization time"
        $GOPTEST $GRAPH11 -tgoal 0.1 $GRAPHHEAD $GRAPHTAIL -sync
    endif
    #
    if ( $BCAST == 1 ) then
        #
        # Broadcast tsets
        if ($NP == 2) echo "# Broadcast tests"
        $GOPTEST $GRAPH11 -bcast -tgoal 0.1 $GRAPHHEAD $GRAPHTAIL
        if ( $SHORT == 0 ) then 
            $GOPTEST $GRAPH12 -bcast -tgoal 0.1 \
	     	         -size 1024 16384 2048 $GRAPHHEAD $GRAPHTAIL
            $GOPTEST $GRAPH21 -bcast -tgoal 0.5 \
	        -size 16384 65536 16384  -reps 100 $GRAPHHEAD $GRAPHTAIL
            #$GOPTEST $GRAPH22 -bcast -tgoal 1.0 \
            #	-size 65536 1048576 65536 -reps 100 $GRAPHHEAD $GRAPHTAIL
        endif
    endif
#
# Collective operation tests
#
if ( $DSUM == 1 ) then
    if ($NP == 2) echo "# Reduction tests"
    #
    # We set sizes on these since they are with respect to either ints or 
    # doubles, not bytes
    $GOPTEST $GRAPH11 -dsum $GRAPHHEAD $GRAPHTAIL
    if ( $SHORT == 0 ) then
        $GOPTEST $GRAPH12 -dsum -tgoal 0.1 \
	    -size 1024 16384 2048 $GRAPHHEAD $GRAPHTAIL
    endif
endif
#
# End of the while on NP
@ NP = $NP * $MULT + $ADD
set GRAPHHEAD = "-nohead"
# If we are the second to last iteration, remove the -notail option.
if ($NP * $MULT + $ADD > $MAXNP) set GRAPHTAIL = ""
end 
