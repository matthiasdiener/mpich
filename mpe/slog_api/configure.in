dnl
dnl Caching is usually WRONG for systems with cross-mounted file systems
dnl (the cache file may correspond to a different system).  Since configure
dnl is not on a performance-critical path, go for robustness over speed.
dnl
define([AC_CACHE_LOAD], )dnl
define([AC_CACHE_SAVE], )dnl
dnl
dnl
AC_INIT()
#
AC_ARG_ENABLE(strict,
[--enable-strict  - Turn on strict compilation testing when using gcc],
COPTIONS="${COPTIONS} -Wall -O -Wstrict-prototypes -Wmissing-prototypes -DGCC_WALL")
#
echo "Configuring SLOG with $ac_configure_args"
#
AC_CONFIG_HEADER( slog_config.h )
AC_HEADER_STDC
AC_CHECK_HEADERS( unistd.h stdlib.h string.h ctype.h stdarg.h )
PAC_HEADER_STDARG(,AC_MSG_ERROR([slog_api requires C-standard stdarg]))
#
MAKE=${MAKE:-"make"}
SLOGNAME=slog
CFLAGS="-DHAVE_SLOGCONF_H $CFLAGS"
#
AC_DEFINE( CHECKRECDEFS )
AC_DEFINE( CHECKPROFILE )
AC_DEFINE( CHECKTIMEORDER )
AC_DEFINE( COMPRESSION )
# AC_DEFINE( NOINSTRUCTION )
#
# Find the home directory if not specified
if test "X$srcdir" != "X." -a -s $srcdir/Makefile.in ; then 
    SLOG_TRIAL=$srcdir
else
    PAC_GETWD(SLOG_TRIAL,src/Makefile.in)
fi
SLOG_HOME=$SLOG_TRIAL
 
  
CC=${CC:-cc}

AC_ARG_WITH( cc, 
[--with-cc=SLOG_CC                  - pass the preferred CC compiler, 
                                     e.g. --with-cc=xlc.  This will overide 
                                     the environmental variable CC.],
SLOG_CC="$withval" )
if test -n "$SLOG_CC" ; then
    CC="$SLOG_CC"
fi

#  Check if it is mpcc, then use xlc instead; this is for AIX
if test -n "` echo $CC | grep mpcc `" -o -n "` echo $CC | grep mpCC `" ; then
    CC="xlc"
fi

AC_ARG_WITH( cflags, 
[--with-cflags=SLOG_CFLAGS          - pass extra CFLAGS to the CC compiler 
                                     such as optimization flags or special 
                                     machine dependent flags, e.g. -64 for 
                                     IRIX64 platforms], 
SLOG_CFLAGS="$withval" ) 
if test -n "$SLOG_CFLAGS" ; then 
    CFLAGS="$CFLAGS $SLOG_CFLAGS"
fi
 
AC_ARG_ENABLE( g, 
[--enable-g                         - Turn on/off the -g flag,
                                     default=disabled],
, enable_g=no )
if test "$enable_g" = "yes" ; then
    if test -z "`echo $CFLAGS | grep "\-g"`"; then
        CFLAGS="-g $CFLAGS" 
    fi
fi
 
AC_ARG_ENABLE( debugcheck, 
[--enable-debugcheck                - Turn on/off the debugging and 
                                     diagnostic code, default=disabled],
, enable_debugcheck=no )
if test "$enable_debugcheck" = "yes" ; then
    AC_DEFINE( DEBUG )
fi

# echo "prefix = $prefix"
# echo "exec_prefix = $exec_prefix"
# echo "includedir = $includedir"
# echo "libdir = $libdir"
# echo "bindir = $bindir"
# echo "mandir = $mandir"
# echo "htmldir = $htmldir"

#
# Create the "autoconf" style directory names...
#
# top_srcdir is the top level source code directory (slog home)
if test -z "$top_srcdir" -o "$top_srcdir" = "." ; then 
    top_srcdir=$SLOG_HOME 
fi
AC_SUBST(top_srcdir)

if test -z "$htmldir" ; then
    htmldir=$mandir/../html
fi
AC_SUBST(htmldir)

#
# libbuild_dir is used to build the libraries in before they are installed.
# binbuild_dir is for the scripts/programs
# includebuild_dir is for all user header files
# manbuild_dir is for all SLOG-API manpages 
# htmlbuild_dir is for all SLOG-API html pages
#
rootbuild_dir=`pwd`
for dir in lib bin include ; do
    dirname=${dir}build_dir
    eval dirvalue=\$"$dirname"
    eval $dirname=$rootbuild_dir/$dir
done
for dir in man html ; do
    dirname=${dir}build_dir
    eval dirvalue=\$"$dirname"
    eval $dirname=$rootbuild_dir/doc/$dir
done

AC_ARG_ENABLE( build_include, [--enable-build_include             - Turn on/off the building of include files], , enable_build_include=yes )
if test "$enable_build_include" = "no" ; then
    includebuild_dir=
fi
AC_ARG_ENABLE( build_man, [--enable-build_man                 - Turn on/off the building of man files], , enable_build_man=yes )
if test "$enable_build_man" = "no" ; then
    manbuild_dir=
fi
AC_ARG_ENABLE( build_html, [--enable-build_html                - Turn on/off the building of html files], , enable_build_html=yes )
if test "$enable_build_html" = "no" ; then
    htmlbuild_dir=
fi

for dir in lib bin include man html ; do
    dirname=${dir}build_dir
    eval dirvalue=\$"$dirname"
    if test -n "$dirvalue" ; then 
        if test ! -d $dirvalue ; then
            if mkdir -p $dirvalue ; then
                :
            else
                AC_MSG_ERROR( [Could not create directory $dirvalue] )
            fi
        fi
    fi 
done

AC_SUBST(includebuild_dir)
AC_SUBST(libbuild_dir)
AC_SUBST(binbuild_dir)
AC_SUBST(manbuild_dir)
AC_SUBST(htmlbuild_dir)

#
# Fixup for make
PAC_MAKE_IS_GNUMAKE
PAC_MAKE_IS_BSD44
PAC_MAKE_IS_OSF
PAC_MAKE_VPATH
AC_SUBST(MAKE)
#
if test ! -s include/slog.h -a -z "$VPATH" ; then
    AC_MSG_ERROR([No virtual MAKE path command found.
	You may need to set your make command
	The GNU make (sometimes available as gnumake) can be used.])
fi

#
AC_PROG_CC

AC_C_CONST
#

AC_CHECK_SIZEOF( char, $CROSS_SIZEOF_CHAR )
if test "$ac_cv_sizeof_char" != "1" ; then
    AC_MSG_ERROR( [**** Unexpected byte length for char.  Abort!!!] )
fi
AC_CHECK_SIZEOF( short, $CROSS_SIZEOF_SHORT )
if test "$ac_cv_sizeof_short" != "2" ; then
    AC_MSG_ERROR( [**** Unexpected byte length for short.  Abort!!!] )
fi
AC_CHECK_SIZEOF( int, $CROSS_SIZEOF_INT )
if test "$ac_cv_sizeof_int" != "4" ; then
    AC_MSG_ERROR( [**** Unexpected byte length for int.  Abort!!!] )
fi
AC_CHECK_SIZEOF( long long, $CROSS_SIZEOF_LONG_LONG )
if test "$ac_cv_sizeof_long_long" != "8" ; then
    AC_MSG_ERROR( [**** Unexpected byte length for long long.  Abort!!!] )
fi

#   -- Check if byteswapping needs to be done.
if test "$cross_compiling" = "yes" -o "$cross_compiling" = 1 ; then
    AC_MSG_CHECKING([for byte ordering])
    if test "X$CROSS_BIGENDIAN" != "X" ; then
        if test "$CROSS_BIGENDIAN" = "true" \
             -o "$CROSS_BIGENDIAN" = "false" ; then
            if test "$CROSS_BIGENDIAN" = "true" ; then
                AC_DEFINE(WORDS_BIGENDIAN)
                AC_MSG_RESULT([defined to be big endian])
            else
                AC_MSG_RESULT([defined to be little endian])
            fi
        else
            AC_DEFINE(WORDS_BIGENDIAN)
            AC_MSG_RESULT([Unknown value $CROSS_BIGENDIAN! Assumed big endian])
        fi
    else
        AC_DEFINE(WORDS_BIGENDIAN)
        AC_MSG_RESULT([Undefined CROSS_BIGENDIAN! Assumed big endian])
    fi
else
    AC_C_BIGENDIAN
fi

#
AC_MSG_CHECKING([if the C compiler treats stdarg consistently])
AC_TRY_COMPILE( [
#include <stdarg.h>

typedef  unsigned int    uint32;
typedef  unsigned short  uint16;
typedef  unsigned char   uint8;

void test_fn( const uint32 ui32, ... )
{
        va_list       ap;
        uint16        ui16;
        uint8         ui8;

        va_start( ap, ui32 );
        ui16      = va_arg( ap, uint16 );
        ui8       = va_arg( ap, uint8 );
        va_end( ap );
} ], 
[ 
        uint32        ui32;
        uint16        ui16;
        uint8         ui8;

        test_fn( ui32, ui16, ui8 );
], prompt_va_arg_to_int=no, prompt_va_arg_to_int=yes )
if test "$prompt_va_arg_to_int" = "yes" ; then
    AC_MSG_RESULT(no)
    AC_DEFINE(GCC296_VA_ARG_FIX)
else
    AC_MSG_RESULT(yes)
fi

#   -- Check if there is any memory leak checking tool like "electric fence"
if test "$enable_debugcheck" = "yes"; then
    AC_CHECK_LIB( efence, malloc, DEBUG_LIBS="-lefence" )
fi
##if test "$enable_debugcheck" = "yes"; then
##    AC_MSG_CHECKING( whether electric fence exists )
##    TMP_LIBS=$LIBS
##    LIBS="-lefence"
##    AC_TRY_LINK( [#include <stdlib.h>],
##                 [ int *iptr; iptr = (int*) malloc(sizeof(int)) ],
##                 efence_exist=yes, efence_exist=no )
##    if test "$efence_exist" = "yes" ; then
##        AC_MSG_RESULT(yes)
##        DEBUG_LIBS="-lefence"
##    else
##        AC_MSG_RESULT(no)
##        DEBUG_LIBS=""
##    fi
##    LIBS=$TMP_LIBS
##fi
#
AC_FUNC_ALLOCA
#
#
CFLAGS="$CFLAGS $COPTIONS"

AC_PROG_LN_S
AC_PROG_RANLIB
AC_CHECK_PROG(AR, ar, ar, ;)
#
# Try to workaround the va_arg bug found gcc-2.96 
#
if test -z "$CLINKER" ; then CLINKER="$CC" ; fi
AC_SUBST(LN_S)
AC_SUBST(CC)
AC_SUBST(CLINKER)
AC_SUBST(AR)
AC_SUBST(RANLIB)
AC_SUBST(SLOGNAME)
AC_SUBST(SLOG_HOME)
AC_SUBST(CFLAGS)
AC_SUBST(DEBUG_LIBS)
# Make sure that there is no old slog_config.h file
rm -f $srcdir/slog_config.h
AC_OUTPUT(Makefile src/Makefile sbin/install-slog)

for script in "sbin/install-slog"; do
    if test -f "$script" ; then
        chmod a+x $script
    else
        AC_MSG_ERROR([*** $script is not a valid file - Make sure you have configured with a valid SLOG-API home directory])
    fi
done
